buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'nu.studer:gradle-jooq-plugin:2.0.11'
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'nu.studer.jooq'

group = 'project.pilot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('mysql:mysql-connector-java:8.0.12')
    runtime('org.springframework.boot:spring-boot-devtools')
    compile('com.google.guava:guava:26.0-jre')
    compileOnly('org.projectlombok:lombok')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    jooqRuntime('mysql:mysql-connector-java:8.0.12')
    compile('org.jooq:jooq')
}

jooq {
    version = '3.10.8' // the default (can be omitted)
    edition = 'OSS'    // the default (can be omitted), other allowed values are PRO, PRO_JAVA_6, and TRIAL
    sample(sourceSets.main) {
        jdbc {
            driver = 'com.mysql.jdbc.Driver'
            url = 'jdbc:mysql://192.168.99.100:3306/jooq_pilot'
            user = 'local_user'
            password = 'password'
        }
        generator {
            database {
                inputSchema = 'jooq_pilot'
            }
            generate {
                javaTimeTypes = true
            }
            target {
                packageName = 'project.pilot.api.jooq'
                directory = 'src/main/java'
            }
        }
    }

}